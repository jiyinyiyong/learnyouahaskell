<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
"http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
    <head>
        <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
        <style>
            @import url(../css/style.css);
            @import url(../css/feedback.css);
        </style>
        <script src="../js/jquery.js"></script>
        <script src="../js/jquery.chili-2.2.js"></script>
        <script src="../js/script.js"></script>
        <script src="../js/html2canvas.js"></script>
        <script src="../js/jsfeedback.js"></script>
        <script>
             ChiliBook.recipeFolder = "../js/chili/";  
             ChiliBook.automaticSelector = "pre";
        </script>
        <script>
            $(function(){
                $('#feedback').click(function(){
                    $('body').feedback();
                })
            });
        </script>
        <script type="text/javascript">
            var _gaq = _gaq || [];
            _gaq.push(['_setAccount', 'UA-32830659-1']);
            _gaq.push(['_trackPageview']);

            (function() {
                var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
                ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
                    var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
            })();
        </script>
        <title>构造我们自己的 Types 和 Typeclasses</title>
    </head>
    <body>
        <div id="header">
            <img id="beta" alt="beta" src="../img/beta.png" />
        </div>

        <div id="main">
            <ul class="nav">
                <li class="left">  <img src="../img/prv.png"></img><a href="modules.html">模组 (Modules)</a></li>
                <li class="center"><a href="chapters.html">Index</a></li>
                <li class="right"> <a href="input-and-output.html">输入与输出</a><img src="../img/nxt.png"></img></li>
            </ul>
            <a name="构造我们自己的 Types 和 Typeclasses"></a><h1>构造我们自己的 Types 和 Typeclasses</h1><a name="Algebraic Data Types 入门"></a><h2>Algebraic Data Types 入门</h2><p>在前面的章节中，我们谈了一些 Haskell 内建的型别和 Typeclass。而在本章中，我们将学习构造型别和 Typeclass 的方法。</p><p>我们已经见识过许多型别，如<code>Bool</code>、<code>Int</code>、<code>Char</code>、<code>Maybe</code>等等，不过在 Haskell 中该如何构造自己的型别呢？好问题，一种方法是使用<b>data</b>关键字。首先我们来看看<code>Bool</code>在标准函式库中的定义：</p><pre class="code">data Bool = False | True</pre><p><b>data</b>表示我们要定义一个新的型别。<code>=</code>的左端标明型别的名称即<code>Bool</code>，<code>=</code>的右端就是<b>值构造子</b>(<b>Value Constructor</b>)，它们明确了该型别可能的值。<code>|</code>读作"或"，所以可以这样阅读该声明：<code>Bool</code>型别的值可以是<code>True</code>或<code>False</code>。型别名和值构造子的首字母必大写。</p><p>相似，我们可以假想<code>Int</code>型别的声明：</p><pre class="code">data Int = -2147483648 | -2147483647 | ... | -1 | 0 | 1 | 2 | ... | 2147483647</pre><img src="../img/caveman.png" style="float:right"></img><p>头尾两个值构造子分别表示了<code>Int</code>型别的最小值和最大值，注意到真正的型别宣告不是长这个样子的，这样写只是为了便于理解。我们用省略号表示中间省略的一大段数字。</p><p>我们想想 Haskell 中图形的表示方法。表示圆可以用一个 Tuple，如<code>(43.1,55.0,10.4)</code>，前两项表示圆心的位置，末项表示半径。听著不错，不过三维向量或其它什么东西也可能是这种形式！更好的方法就是自己构造一个表示图形的型别。假定图形可以是圆 (Circle) 或长方形 (Rectangle)：</p><pre class="code">data Shape = Circle Float Float Float | Rectangle Float Float Float Float</pre><p>这是啥，想想？<code>Circle</code>的值构造子有三个项，都是 Float。可见我们在定义值构造子时，可以在后面跟几个型别表示它包含值的型别。在这里，前两项表示圆心的坐标，尾项表示半径。<code>Rectangle</code>的值构造子取四个<code>Float</code>项，前两项表示其左上角的坐标，后两项表示右下角的坐标。</p><p>谈到"项" (field)，其实应为"参数" (parameters)。值构造子的本质是个函数，可以返回一个型别的值。我们看下这两个值构造子的型别声明：</p><pre class="code">ghci> :t Circle  
Circle :: Float -> Float -> Float -> Shape  
ghci> :t Rectangle  
Rectangle :: Float -> Float -> Float -> Float -> Shape</pre><p>Cool，这么说值构造子就跟普通函数并无二致咯，谁想得到？我们写个函数计算图形面积：</p><pre class="code">surface :: Shape -> Float  
surface (Circle _ _ r) = pi * r ^ 2  
surface (Rectangle x1 y1 x2 y2) = (abs $ x2 - x1) * (abs $ y2 - y1)</pre><p>值得一提的是，它的型别声明表示了该函数取一个<code>Shape</code>值并返回一个<code>Float</code>值。写<code>Circle -> Float</code>是不可以的，因为<code>Circle</code>并非型别，真正的型别应该是<code>Shape</code>。这与不能写<code>True->False</code>的道理是一样的。再就是，我们使用的模式匹配针对的都是值构造子。之前我们匹配过<code>[]</code>、<code>False</code>或<code>5</code>，它们都是不包含参数的值构造子。</p><p>我们只关心圆的半径，因此不需理会表示坐标的前两项：</p><pre class="code">ghci> surface $ Circle 10 20 10  
314.15927  
ghci> surface $ Rectangle 0 0 100 100  
10000.0</pre><p>Yay，it works！不过我们若尝试输出<code>Circle 10 20</code>到控制台，就会得到一个错误。这是因为 Haskell 还不知道该型别的字元串表示方法。想想，当我们往控制台输出值的时候，Haskell 会先呼叫<code>show</code>函数得到这个值的字元串表示才会输出。因此要让我们的<code>Shape</code>型别成为Show型别类的成员。可以这样修改：</p><pre class="code">data Shape = Circle Float Float Float | Rectangle Float Float Float Float deriving (Show)</pre><p>先不去深究<b>deriving</b>(派生)，可以先这样理解：若在<code>data</code>声明的后面加上<code>deriving (Show)</code>，那 Haskell 就会自动将该型别至于<code>Show</code>型别类之中。好了，由于值构造子是个函数，因此我们可以拿它交给<code>map</code>，拿它不全呼叫，以及普通函数能做的一切。</p><pre class="code">ghci> Circle 10 20 5  
Circle 10.0 20.0 5.0  
ghci> Rectangle 50 230 60 90  
Rectangle 50.0 230.0 60.0 90.0</pre><p>我们若要取一组不同半径的同心圆，可以这样：</p><pre class="code">ghci> map (Circle 10 20) [4,5,6,6]  
[Circle 10.0 20.0 4.0,Circle 10.0 20.0 5.0,Circle 10.0 20.0 6.0,Circle 10.0 20.0 6.0]</pre><p>我们的型别还可以更好。增加加一个表示二维空间中点的型别，可以让我们的<code>Shape</code>更加容易理解：</p><pre class="code">data Point = Point Float Float deriving (Show)  
data Shape = Circle Point Float | Rectangle Point Point deriving (Show)</pre><p>注意下<code>Point</code>的定义，它的型别与值构造子用了相同的名字。没啥特殊含义，实际上，在一个型别含有唯一值构造子时这种重名是很常见的。好的，如今我们的<code>Circle</code>含有两个项，一个是<code>Point</code>型别，一个是<code>Float</code>型别，好作区分。<code>Rectangle</code>也是同样，我们得修改<code>surface</code>函数以适应型别定义的变动。</p><pre class="code">surface :: Shape -> Float  
surface (Circle _ r) = pi * r ^ 2  
surface (Rectangle (Point x1 y1) (Point x2 y2)) = (abs $ x2 - x1) * (abs $ y2 - y1)</pre><p>唯一需要修改的地方就是模式。在<code>Circle</code>的模式中，我们无视了整个<code>Point</code>。而在<code>Rectangle</code>的模式中，我们用了一个嵌套的模式来取得<code>Point</code>中的项。若出于某原因而需要整个<code>Point</code>，那么直接匹配就是了。</p><pre class="code">ghci> surface (Rectangle (Point 0 0) (Point 100 100))  
10000.0  
ghci> surface (Circle (Point 0 0) 24)  
1809.5574</pre><p>表示移动一个图形的函数该怎么写？ 它应当取一个<code>Shape</code>和表示位移的两个数，返回一个位于新位置的图形。</p><pre class="code">nudge :: Shape -> Float -> Float -> Shape  
nudge (Circle (Point x y) r) a b = Circle (Point (x+a) (y+b)) r  
nudge (Rectangle (Point x1 y1) (Point x2 y2)) a b = Rectangle (Point (x1+a) (y1+b)) (Point (x2+a) (y2+b))</pre><p>简洁明了。我们再给这一<code>Shape</code>的点加上位移的量。</p><pre class="code">ghci> nudge (Circle (Point 34 34) 10) 5 10  
Circle (Point 39.0 44.0) 10.0</pre><p>如果不想直接处理<code>Point</code>，我们可以搞个辅助函数 (auxilliary function)，初始从原点创建图形，再移动它们。</p><pre class="code">baseCircle :: Float -> Shape  
baseCircle r = Circle (Point 0 0) r  

baseRect :: Float -> Float -> Shape  
baseRect width height = Rectangle (Point 0 0) (Point width height)</pre><pre class="code">ghci> nudge (baseRect 40 100) 60 23  
Rectangle (Point 60.0 23.0) (Point 100.0 123.0)</pre><p>毫无疑问，你可以把你的数据型别导出到模组中。只要把你的型别与要导出的函数写到一起就是了。再在后面跟个括号，列出要导出的值构造子，用逗号隔开。如要导出所有的值构造子，那就写个..。</p><p>若要将这里定义的所有函数和型别都导出到一个模组中，可以这样：</p><pre class="code">module Shapes   
( Point(..)  
, Shape(..)  
, surface  
, nudge  
, baseCircle  
, baseRect  
) where</pre><p>一个<code>Shape</code>(..)，我们就导出了<code>Shape</code>的所有值构造子。这一来无论谁导入我们的模组，都可以用<code>Rectangle</code>和<code>Circle</code>值构造子来构造<code>Shape</code>了。这与写<code>Shape(Rectangle,Circle)</code>等价。</p><p>我们可以选择不导出任何<code>Shape</code>的值构造子，这一来使用我们模组的人就只能用辅助函数<code>baseCircle</code>和<code>baseRect</code>来得到<code>Shape</code>了。<code>Data.Map</code>就是这一套，没有<code>Map.Map [(1,2),(3,4)]</code>，因为它没有导出任何一个值构造子。但你可以用，像<code>Map.fromList</code>这样的辅助函数得到<code>map</code>。应该记住，值构造子只是函数而已，如果不导出它们，就拒绝了使用我们模组的人呼叫它们。但可以使用其他返回该型别的函数，来取得这一型别的值。</p><p>不导出数据型别的值构造子隐藏了他们的内部实现，令型别的抽象度更高。同时，我们模组的使用者也就无法使用该值构造子进行模式匹配了。</p><a name="Record Syntax"></a><h2>Record Syntax</h2><p>OK，我们需要一个数据型别来描述一个人，得包含他的姓、名、年龄、身高、体重、电话号码以及最爱的冰激淋。我不知你的想法，不过我觉得要了解一个人，这些资料就够了。就这样，实现出来！</p><pre class="code">data Person = Person String String Int Float String String deriving (Show)</pre><p>O~Kay，第一项是名，第二项是姓，第三项是年龄，等等。我们造一个人：</p><pre class="code">ghci> let guy = Person "Buddy" "Finklestein" 43 184.2 "526-2928" "Chocolate"  
ghci> guy  
Person "Buddy" "Finklestein" 43 184.2 "526-2928" "Chocolate"</pre><p>貌似很酷，就是难读了点儿。弄个函数得人的某项资料又该如何？如姓的函数，名的函数，等等。好吧，我们只能这样：</p><pre class="code">firstName :: Person -> String  
firstName (Person firstname _ _ _ _ _) = firstname  

lastName :: Person -> String  
lastName (Person _ lastname _ _ _ _) = lastname  

age :: Person -> Int  
age (Person _ _ age _ _ _) = age  

height :: Person -> Float  
height (Person _ _ _ height _ _) = height  

phoneNumber :: Person -> String  
phoneNumber (Person _ _ _ _ number _) = number  

flavor :: Person -> String  
flavor (Person _ _ _ _ _ flavor) = flavor</pre><p>唔，我可不愿写这样的程式码！虽然 it works，但也太无聊了哇。</p><pre class="code">ghci> let guy = Person "Buddy" "Finklestein" 43 184.2 "526-2928" "Chocolate"  
ghci> firstName guy  
"Buddy"  
ghci> height guy  
184.2  
ghci> flavor guy  
"Chocolate"</pre><p>你可能会说，一定有更好的方法！呃，抱歉，没有。</p><p>开个玩笑，其实有的，哈哈哈～Haskell 的发明者都是天才，早就料到了此类情形。他们引入了一个特殊的型别，也就是刚才提到的更好的方法 --<b>Record Syntax</b>。</p><pre class="code">data Person = Person { firstName :: String  
                     , lastName :: String  
                     , age :: Int  
                     , height :: Float  
                     , phoneNumber :: String  
                     , flavor :: String  
                     } deriving (Show)</pre><p>与原先让那些项一个挨一个的空格隔开不同，这里用了花括号<code>{}</code>。先写出项的名字，如<code>firstName</code>，后跟两个冒号(也叫 Raamayim Nekudotayim，哈哈~(译者不知道什么意思~囧))，标明其型别，返回的数据型别仍与以前相同。这样的好处就是，可以用函数从中直接按项取值。通过 Record Syntax，Haskell 就自动生成了这些函数：<code>firstName</code>,<code>lastName</code>,<code>age</code>,<code>height</code>,<code>phoneNumber</code>和<code>flavor</code>。</p><pre class="code">ghci> :t flavor  
flavor :: Person -> String  
ghci> :t firstName  
firstName :: Person -> String</pre><p>还有个好处，就是若派生 (deriving) 到<code>Show</code>型别类，它的显示是不同的。假如我们有个型别表示一辆车，要包含生产商、型号以及出场年份：</p><pre class="code">data Car = Car String String Int deriving (Show)</pre><pre class="code">ghci> Car "Ford" "Mustang" 1967  
Car "Ford" "Mustang" 1967</pre><p>若用 Record Syntax，就可以得到像这样的新车：</p><pre class="code">data Car = Car {company :: String, model :: String, year :: Int} deriving (Show)</pre><pre class="code">ghci> Car {company="Ford", model="Mustang", year=1967}  
Car {company = "Ford", model = "Mustang", year = 1967}</pre><p>这一来在造车时我们就不必关心各项的顺序了。</p><p>表示三维向量之类简单数据，<code>Vector = Vector Int Int Int</code>就足够明白了。但一个值构造子中若含有很多个项且不易区分，如一个人或者一辆车啥的，就应该使用 Record Syntax。</p><a name="Type parameters"></a><h2>Type parameters</h2><p>值构造子可以取几个参数产生一个新值，如<code>Car</code>的构造子是取三个参数返回一个<code>Car</code>。与之相似，型别构造子可以取型别作参数，产生新的型别。这咋一听貌似有点深奥，不过实际上并不复杂。如果你对 C++ 的模板有了解，就会看到很多相似的地方。我们看一个熟悉的型别，好对型别参数有个大致印象：</p><pre class="code">data Maybe a = Nothing | Just a</pre><img src="../img/yeti.png" style="float:right"></img><p>这里的a就是个型别参数。也正因为有了它，<code>Maybe</code>就成为了一个型别构造子。在它的值不是<code>Nothing</code>时，它的型别构造子可以搞出<code>Maybe Int</code>，<code>Maybe String</code>等等诸多型别。但只一个<code>Maybe</code>是不行的，因为它不是型别，而是型别构造子。要成为真正的型别，必须得把它需要的型别参数全部填满。</p><p>所以，如果拿<code>Char</code>作参数交给<code>Maybe</code>，就可以得到一个<code>Maybe Char</code>的型别。如，<code>Just 'a'</code>的型别就是<code>Maybe Char</code>。</p><p>你可能并未察觉，在遇见 Maybe 之前我们早就接触到型别参数了。它便是 List 型别。这里面有点语法糖，List 型别实际上就是取一个参数来生成一个特定型别，这型别可以是<code>[Int]</code>，<code>[Char]</code>也可以是<code>[String]</code>，但不会跟在<code>[]</code>的后面。</p><p>把玩一下<code>Maybe</code>！</p><pre class="code">ghci> Just "Haha"  
Just "Haha"  
ghci> Just 84  
Just 84  
ghci> :t Just "Haha"  
Just "Haha" :: Maybe [Char]  
ghci> :t Just 84  
Just 84 :: (Num t) => Maybe t  
ghci> :t Nothing  
Nothing :: Maybe a  
ghci> Just 10 :: Maybe Double  
Just 10.0</pre><p>型别参数很实用。有了它，我们就可以按照我们的需要构造出不同的型别。若执行<code>:t Just "Haha"</code>，型别推导引擎就会认出它是个<code>Maybe [Char]</code>，由于<code>Just a</code>里的<code>a</code>是个字元串，那么<code>Maybe a</code>里的<code>a</code>一定也是个字元串。</p><img src="../img/meekrat.png" style="float:right"></img><p>注意下，<code>Nothing</code>的型别为<code>Maybe a</code>。它是多态的，若有函数取<code>Maybe Int</code>型别的参数，就一概可以传给它一个<code>Nothing</code>，因为<code>Nothing</code>中不包含任何值。<code>Maybe a</code>型别可以有<code>Maybe Int</code>的行为，正如<code>5</code>可以是<code>Int</code>也可以是<code>Double</code>。与之相似，空 List 的型别是<code>[a]</code>，可以与一切 List 打交道。因此，我们可以<code>[1,2,3]++[]</code>，也可以<code>["ha","ha,","ha"]++[]</code>。</p><p>型别参数有很多好处，但前提是用对了地方才行。一般都是不关心型别里面的内容，如<code>Maybe a</code>。一个型别的行为若有点像是容器，那么使用型别参数会是个不错的选择。我们完全可以把我们的<code>Car</code>型别从</p><pre class="code">data Car = Car { company :: String
                 , model :: String
                 , year :: Int
                 } deriving (Show)</pre><p>改成：</p><pre class="code">data Car a b c = Car { company :: a
                       , model :: b
                       , year :: c
                        } deriving (Show)</pre><p>但是，这样我们又得到了什么好处？回答很可能是，一无所得。因为我们只定义了处理<code>Car String String Int</code>型别的函数，像以前，我们还可以弄个简单函数来描述车的属性。</p><pre class="code">tellCar :: Car -> String
tellCar (Car {company = c, model = m, year = y}) = "This " ++ c ++ " " ++ m ++ " was made in " ++ show y</pre><pre class="code">ghci> let stang = Car {company="Ford", model="Mustang", year=1967}  
ghci> tellCar stang  "This Ford Mustang was made in 1967"</pre><p>可爱的小函数！它的型别声明得很漂亮，而且工作良好。好，如果改成<code>Car a b c</code>又会怎样？</p><pre class="code">tellCar :: (Show a) => Car String String a -> String  
tellCar (Car {company = c, model = m, year = y}) = "This " ++ c ++ " " ++ m ++ " was made in " ++ show y</pre><p>我们只能强制性地给这个函数安一个<code>(Show a) => Car String String a</code>的型别约束。看得出来，这要繁复得多。而唯一的好处貌似就是，我们可以使用<code>Show</code>型别类的<code>instance</code>来作<code>a</code>的型别。</p><pre class="code">ghci> tellCar (Car "Ford" "Mustang" 1967)  
"This Ford Mustang was made in 1967"  
ghci> tellCar (Car "Ford" "Mustang" "nineteen sixty seven")  
"This Ford Mustang was made in \"nineteen sixty seven\""  
ghci> :t Car "Ford" "Mustang" 1967  
Car "Ford" "Mustang" 1967 :: (Num t) => Car [Char] [Char] t  
ghci> :t Car "Ford" "Mustang" "nineteen sixty seven"  
Car "Ford" "Mustang" "nineteen sixty seven" :: Car [Char] [Char] [Char]</pre><p>其实在现实生活中，使用<code>Car String String Int</code>在大多数情况下已经满够了。所以给<code>Car</code>型别加型别参数貌似并没有什么必要。通常我们都是都是在一个型别中包含的型别并不影响它的行为时才引入型别参数。一组什么东西组成的 List 就是一个 List，它不关心里面东西的型别是啥，然而总是工作良好。若取一组数字的和，我们可以在后面的函数体中明确是一组数字的 List。Maybe 与之相似，它表示可以有什么东西可以没有，而不必关心这东西是啥。</p><p>我们之前还遇见过一个型别参数的应用，就是<code>Data.Map</code>中的<code>Map k v</code>。<code>k</code>表示 Map 中键的型别，<code>v</code>表示值的型别。这是个好例子，Map 中型别参数的使用允许我们能够用一个型别索引另一个型别，只要键的型别在<code>Ord</code>型别类就行。如果叫我们自己定义一个 Map 型别，可以在<code>data</code>声明中加上一个型别类的约束。</p><pre class="code">data (Ord k) => Map k v = ...</pre><p>然而 Haskell 中有一个严格的约定，那就是永远不要在<code>data</code>声明中添加型别约束。为啥？嗯，因为这样没好处，反而得写更多不必要的型别约束。<code>Map k v</code>要是有<code>Ord k</code>的约束，那就相当于假定每个 Map 的相关函数都认为<code>k</code>是可排序的。若不给数据型别加约束，我们就不必给那些不关心键是否可排序的函数另加约束了。这类函数的一个例子就是<code>toList</code>，它只是把一个 Map 转换为关联 List 罢了，型别声明为<code>toList :: Map k v -> [(k, v)]</code>。要是加上型别约束，就只能是<code>toList :: (Ord k) =>Map k a -> [(k,v)]</code>，明显没必要嘛。</p><p>所以说，永远不要在<code>data</code>声明中加型别约束 --- 即便看起来没问题。免得在函数声明中写出过多无畏的型别约束。</p><p>我们实现个表示三维向量的型别，再给它加几个处理函数。我么那就给它个型别参数，虽然大多数情况都是数值型，不过这一来它就支持了多种数值型别。</p><pre class="code">data Vector a = Vector a a a deriving (Show)    
vplus :: (Num t) => Vector t -> Vector t -> Vector t  
(Vector i j k) `vplus` (Vector l m n) = Vector (i+l) (j+m) (k+n)    
vectMult :: (Num t) => Vector t -> t -> Vector t  
(Vector i j k) `vectMult` m = Vector (i*m) (j*m) (k*m)    
scalarMult :: (Num t) => Vector t -> Vector t -> t  
(Vector i j k) `scalarMult` (Vector l m n) = i*l + j*m + k*n</pre><p><code>vplus</code>用来相加两个向量，即将其所有对应的项相加。<code>scalarMult</code>用来求两个向量的标量积，<code>vectMult</code>求一个向量和一个标量的积。这些函数可以处理<code>Vector Int</code>，<code>Vector Integer</code>，<code>Vector Float</code>等等型别，只要<code>Vector a</code>里的这个<code>a</code>在<code>Num</code>型别类中就行。同样，如果你看下这些函数的型别声明就会发现，它们只能处理相同型别的向量，其中包含的数字型别必须与另一个向量一致。注意，我们并没有在<code>data</code>声明中添加<code>Num</code>的类约束。反正无论怎么著都是给函数加约束。</p><p>再度重申，型别构造子和值构造子的区分是相当重要的。在声明数据型别时，等号=左端的那个是型别构造子，右端的(中间可能有|分隔)都是值构造子。拿<code>Vector t t t -> Vector t t t -> t</code>作函数的型别就会产生一个错误，因为在型别声明中只能写型别，而<code>Vector</code>的型别构造子只有个参数，它的值构造子才是有三个。我们就慢慢耍：</p><pre class="code">ghci> Vector 3 5 8 `vplus` Vector 9 2 8  
Vector 12 7 16  
ghci> Vector 3 5 8 `vplus` Vector 9 2 8 `vplus` Vector 0 2 3  
Vector 12 9 19  
ghci> Vector 3 9 7 `vectMult` 10  
Vector 30 90 70  
ghci> Vector 4 9 5 `scalarMult` Vector 9.0 2.0 4.0  
74.0  
ghci> Vector 2 9 3 `vectMult` (Vector 4 9 5 `scalarMult` Vector 9 2 4)  
Vector 148 666 222</pre><a name="Derived instances"></a><h2>Derived instances</h2><img src="../img/gob.png" style="float:right"></img><p>在<a href="/make-types-and-classes-for-ourselves.html#Typeclasses的第二堂课">Typeclass 101</a>那一节里面，我们了解了 Typeclass 的基础内容。里面提到，型别类就是定义了某些行为的介面。例如，Int 型别是<code>Eq</code>型别类的一个 instance，<code>Eq</code>类就定义了判定相等性的行为。Int 值可以判断相等性，所以 Int 就是<code>Eq</code>型别类的成员。它的真正威力体现在作为<code>Eq</code>介面的函数中，即<code>==</code>和<code>/=</code>。只要一个型别是<code>Eq</code>型别类的成员，我们就可以使用<code>==</code>函数来处理这一型别。这便是为何<code>4==4</code>和<code>"foo"/="bar"</code>这样的表达式都需要作型别检查。</p><p>我们也曾提到，人们很容易把型别类与 Java，Python，C++ 等语言的类混淆。很多人对此都倍感不解，在原先那些语言中，类就像是蓝图，我们可以根据它来创造对象、保存状态并执行操作。而型别类更像是介面，我们不是靠它构造数据，而是给既有的数据型别描述行为。什么东西若可以判定相等性，我们就可以让它成为<code>Eq</code>型别类的 instance。什么东西若可以比较大小，那就可以让它成为<code>Ord</code>型别类的 instance。</p><p>在下一节，我们将看一下如何手工实现型别类中定义函数来构造 instance。现在呢，我们先了解下 Haskell 是如何自动生成这几个型别类的 instance，<code>Eq</code>,<code>Ord</code>,<code>Enum</code>,<code>Bounded</code>,<code>Show</code>,<code>Read</code>。只要我们在构造型别时在后面加个<code>deriving</code>(派生)关键字，Haskell 就可以自动地给我们的型别加上这些行为。</p><p>看这个数据型别：</p><pre class="code">data Person = Person { firstName :: String  
                     , lastName :: String  
                     , age :: Int  
                     }</pre><p>这描述了一个人。我们先假定世界上没有重名重姓又同龄的人存在，好，假如有两个 record，有没有可能是描述同一个人呢？当然可能，我么可以判定姓名年龄的相等性，来判断它俩是否相等。这一来，让这个型别成为<code>Eq</code>的成员就很靠谱了。直接 derive 这个 instance：</p><pre class="code">data Person = Person { firstName :: String  
                     , lastName :: String  
                     , age :: Int  
                     } deriving (Eq)</pre><p>在一个型别 derive 为<code>Eq</code>的 instance 后，就可以直接使用<code>==</code>或<code>/=</code>来判断它们的相等性了。Haskell 会先看下这两个值的值构造子是否一致(这里只是单值构造子)，再用<code>==</code>来检查其中的所有数据(必须都是<code>Eq</code>的成员)是否一致。在这里只有<code>String</code>和 Int，所以是没有问题的。测试下我们的 Eqinstance：</p><pre class="code">ghci> let mikeD = Person {firstName = "Michael", lastName = "Diamond", age = 43}  
ghci> let adRock = Person {firstName = "Adam", lastName = "Horovitz", age = 41}  
ghci> let mca = Person {firstName = "Adam", lastName = "Yauch", age = 44}  
ghci> mca == adRock  
False  
ghci> mikeD == adRock  
False  
ghci> mikeD == mikeD  
True  
ghci> mikeD == Person {firstName = "Michael", lastName = "Diamond", age = 43}  
True</pre><p>自然，<code>Person</code>如今已经成为了<code>Eq</code>的成员，我们就可以将其应用于所有在型别声明中用到<code>Eq</code>类约束的函数了，如<code>elem</code>。</p><pre class="code">ghci> let beastieBoys = [mca, adRock, mikeD]  
ghci> mikeD `elem` beastieBoys  
True</pre><p><code>Show</code>和<code>Read</code>型别类处理可与字元串相互转换的东西。同<code>Eq</code>相似，如果一个型别的构造子含有参数，那所有参数的型别必须都得属于<code>Show</code>或<code>Read</code>才能让该型别成为其 instance。就让我们的<code>Person</code>也成为<code>Read</code>和<code>Show</code>的一员吧。</p><pre class="code">data Person = Person { firstName :: String  
                     , lastName :: String  
                     , age :: Int  
                     } deriving (Eq, Show, Read)</pre><p>然后就可以输出一个<code>Person</code>到控制台了。</p><pre class="code">ghci> let mikeD = Person {firstName = "Michael", lastName = "Diamond", age = 43}  
ghci> mikeD  
Person {firstName = "Michael", lastName = "Diamond", age = 43}  
ghci> "mikeD is: " ++ show mikeD  
"mikeD is: Person {firstName = \"Michael\", lastName = \"Diamond\", age = 43}"</pre><p>如果我们还没让<code>Person</code>型别作为<code>Show</code>的成员就尝试输出它，Haskell 就会向我们抱怨，说它不知道该怎么把它表示成一个字元串。不过现在既然已经 derive 成为了<code>Show</code>的一个 instance，它就知道了。</p><p><code>Read</code>几乎就是与<code>Show</code>相对的型别类，<code>show</code>是将一个值转换成字元串，而<code>read</code>则是将一个字元串转成某型别的值。还记得，使用<code>read</code>函数时我们必须得用型别注释注明想要的型别，否则 Haskell 就不会知道如何转换。</p><pre class="code">ghci> read "Person {firstName =\"Michael\", lastName =\"Diamond\", age = 43}" :: Person  
Person {firstName = "Michael", lastName = "Diamond", age = 43}</pre><p>如果我们<code>read</code>的结果会在后面用到参与计算，Haskell 就可以推导出是一个 Person 的行为，不加注释也是可以的。</p><pre class="code">ghci> read "Person {firstName =\"Michael\", lastName =\"Diamond\", age = 43}" == mikeD  
True</pre><p>也可以<code>read</code>带参数的型别，但必须填满所有的参数。因此<code>read "Just 't'" :: Maybe a</code>是不可以的，<code>read "Just 't'" :: Maybe Char</code>才对。</p><p>很容易想象<code>Ord</code>型别类 derive instance 的行为。首先，判断两个值构造子是否一致，如果是，再判断它们的参数，前提是它们的参数都得是<code>Ord</code>的 instance。<code>Bool</code>型别可以有两种值，<code>False</code>和<code>True</code>。为了了解在比较中程序的行为，我们可以这样想象：</p><pre class="code">data Bool = False | True deriving (Ord)</pre><p>由于值构造子<code>False</code>安排在<code>True</code>的前面，我们可以认为<code>True</code>比<code>False</code>大。</p><pre class="code">ghci> True `compare` False  
GT  
ghci> True > False  
True  
ghci> True 
False</pre><p>在<code>Maybe a</code>数据型别中，值构造子<code>Nothing</code>在<code>Just</code>值构造子前面，所以一个<code>Nothing</code>总要比<code>Just something</code>的值小。即便这个<code>something</code>是<code>-100000000</code>也是如此。</p><pre class="code">ghci> Nothing 
True  
ghci> Nothing > Just (-49999)  
False  
ghci> Just 3 `compare` Just 2  
GT  
ghci> Just 100 > Just 50  
True</pre><p>不过类似<code>Just (*3)</code>,<code>Just(*2)</code>之类的程式码是不可以的。因为<code>(*3)</code>和<code>(*2)</code>都是函数，而函数不是<code>Ord</code>类的成员。</p><p>作枚举，使用数字型别就能轻易做到。不过使用<code>Enmu</code>和<code>Bounded</code>型别类会更好，看下这个型别：</p><pre class="code">data Day = Monday | Tuesday | Wednesday | Thursday | Friday | Saturday | Sunday</pre><p>所有的值构造子都是<code>nullary</code>的(也就是没有参数)，每个东西都有前置子和后继子，我们可以让它成为<code>Enmu</code>型别类的成员。同样，每个东西都有可能的最小值和最大值，我们也可以让它成为<code>Bounded</code>型别类的成员。在这里，我们就同时将它搞成其它可 derive型别类的 instance。再看看我们能拿它做啥：</p><pre class="code">data Day = Monday | Tuesday | Wednesday | Thursday | Friday | Saturday | Sunday   
           deriving (Eq, Ord, Show, Read, Bounded, Enum)</pre><p>由于它是<code>Show</code>和<code>Read</code>型别类的成员，我们可以将这个型别的值与字元串相互转换。</p><pre class="code">ghci> Wednesday  
Wednesday  
ghci> show Wednesday  
"Wednesday"  
ghci> read "Saturday" :: Day  
Saturday</pre><p>由于它是<code>Eq</code>与<code>Ord</code>的成员，因此我们可以拿<code>Day</code>作比较。</p><pre class="code">ghci> Saturday == Sunday  
False  
ghci> Saturday == Saturday  
True  
ghci> Saturday > Friday  
True  
ghci> Monday `compare` Wednesday  
LT</pre><p>它也是<code>Bounded</code>的成员，因此有最早和最晚的一天。</p><pre class="code">ghci> minBound :: Day  
Monday  
ghci> maxBound :: Day  
Sunday</pre><p>它也是<code>Enum</code>的 instance，可以得到前一天和后一天，并且可以对此使用 List 的区间。</p><pre class="code">ghci> succ Monday  
Tuesday  
ghci> pred Saturday  
Friday  
ghci> [Thursday .. Sunday]  
[Thursday,Friday,Saturday,Sunday]  
ghci> [minBound .. maxBound] :: [Day]  
[Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday]</pre><p>那是相当的棒。</p><a name="Type synonyms"></a><h2>Type synonyms</h2><p>在前面我们提到在写型别名的时候，<code>[Char]</code>和<code>String</code>等价，可以互换。这就是由型别别名实现的。型别别名实际上什么也没做，只是给型别提供了不同的名字，让我们的程式码更容易理解。这就是<code>[Char]</code>的别名<code>String</code>的由来。</p><pre class="code">type String = [Char]</pre><p>我们已经介绍过了<code>type</code>关键字，这个关键字有一定误导性，它并不是用来创造新类(这是<code>data</code>关键字做的事情)，而是给一个既有型别提供一个别名。</p><p>如果我们随便搞个函数<code>toUpperString</code>或其他什么名字，将一个字元串变成大写，可以用这样的型别声明<code>toUpperString :: [Char] -> [Char]</code>， 也可以这样<code>toUpperString :: String -> String</code>，二者在本质上是完全相同的。后者要更易读些。</p><p>在前面<code>Data.Map</code>那部分，我们用了一个关联<code>List</code>来表示<code>phoneBook</code>，之后才改成的 Map。我们已经发现了，一个关联 List 就是一组键值对组成的 List。再看下我们<code>phoneBook</code>的样子：</p><pre class="code">phoneBook :: [(String,String)]  
phoneBook =      
    [("betty","555-2938")     
    ,("bonnie","452-2928")     
    ,("patsy","493-2928")     
    ,("lucille","205-2928")     
    ,("wendy","939-8282")     
    ,("penny","853-2492")     
    ]</pre><p>可以看出，<code>phoneBook</code>的型别就是<code>[(String,String)]</code>，这表示一个关联 List 仅是 String 到 String 的映射关系。我们就弄个型别别名，好让它型别声明中能够表达更多信息。</p><pre class="code">type PhoneBook = [(String,String)]</pre><p>现在我们<code>phoneBook</code>的型别声明就可以是<code>phoneBook :: PhoneBook</code>了。再给字元串加上别名：</p><pre class="code">type PhoneNumber = String  
type Name = String  
type PhoneBook = [(Name,PhoneNumber)]</pre><p>Haskell 程序员给 String 加别名是为了让函数中字元串的表达方式及用途更加明确。</p><p>好的，我们实现了一个函数，它可以取一名字和号码检查它是否存在于电话本。现在可以给它加一个相当好看明了的型别声明：</p><pre class="code">inPhoneBook :: Name -> PhoneNumber -> PhoneBook -> Bool  
inPhoneBook name pnumber pbook = (name,pnumber) `elem` pbook</pre><img src="../img/chicken.png" style="float:left"></img><p>如果不用型别别名，我们函数的型别声明就只能是<code>String -> String -> [(String ,String)] -> Bool</code>了。在这里使用型别别名是为了让型别声明更加易读，但你也不必拘泥于它。引入型别别名的动机既非单纯表示我们函数中的既有型别，也不是为了替换掉那些重复率高的长名字型别(如<code>[(String,String)]</code>)，而是为了让型别对事物的描述更加明确。</p><p>型别别名也是可以有参数的，如果你想搞个型别来表示关联 List，但依然要它保持通用，好让它可以使用任意型别作<code>key</code>和<code>value</code>，我们可以这样：</p><pre class="code">type AssocList k v = [(k,v)]</pre><p>好的，现在一个从关联 List 中按键索值的函数型别可以定义为<code>(Eq k) => k -> AssocList k v -> Maybe v. AssocList i</code>。<code>AssocList</code>是个取两个型别做参数生成一个具体型别的型别构造子，如<code>Assoc Int String</code>等等。</p><blockquote><p><b>Fronzie 说</b>：Hey！当我提到具体型别，那我就是说它是完全呼叫的，就像<code>Map Int String</code>。要不就是多态函数中的<code>[a]</code>或<code>(Ord a) => Maybe a</code>之类。有时我和孩子们会说 "Maybe 型别"，但我们的意思并不是按字面来，傻瓜都知道<code>Maybe</code>是型别构造子嘛。只要用一个明确的型别呼叫<code>Maybe</code>，如<code>Maybe String</code>可得一个具体型别。你知道，只有具体型别才可以储存值。</p></blockquote><p>我们可以用不全呼叫来得到新的函数，同样也可以使用不全呼叫得到新的型别构造子。同函数一样，用不全的型别参数呼叫型别构造子就可以得到一个不全呼叫的型别构造子，如果我们要一个表示从整数到某东西间映射关系的型别，我们可以这样：</p><pre class="code">type IntMap v = Map Int v</pre><p>也可以这样：</p><pre class="code">type IntMap = Map Int</pre><p>无论怎样，<code>IntMap</code>的型别构造子都是取一个参数，而它就是这整数指向的型别。</p><p>Oh yeah，如果要你去实现它，很可能会用个<code>qualified import</code>来导入<code>Data.Map</code>。这时，型别构造子前面必须得加上模组名。所以应该写个<code>type IntMap = Map.Map Int</code></p><p>你得保证真正弄明白了型别构造子和值构造子的区别。我们有了个叫<code>IntMap</code>或者<code>AssocList</code>的别名并不意味着我们可以执行类似<code>AssocList [(1,2),(4,5),(7,9)]</code>的程式码，而是可以用不同的名字来表示原先的 List，就像<code>[(1,2),(4,5),(7,9)] :: AssocList Int Int</code>让它里面的型别都是 Int。而像处理普通的 Tuple 构成的那种 List 处理它也是可以的。型别别名(型别依然不变)，只可以在 Haskell 的型别部分中使用，像定义新型别或型别声明或型别注释中跟在::后面的部分。</p><p>另一个很酷的二参型别就是<code>Either a b</code>了，它大约是这样定义的：</p><pre class="code">data Either a b = Left a | Right b deriving (Eq, Ord, Read, Show)</pre><p>它有两个值构造子。如果用了<code>Left</code>，那它内容的型别就是<code>a</code>；用了<code>Right</code>，那它内容的型别就是<code>b</code>。我们可以用它来将可能是两种型别的值封装起来，从里面取值时就同时提供<code>Left</code>和<code>Right</code>的模式匹配。</p><pre class="code">ghci> Right 20  
Right 20  
ghci> Left "w00t"  
Left "w00t"  
ghci> :t Right 'a'  
Right 'a' :: Either a Char  
ghci> :t Left True  
Left True :: Either Bool b</pre><p>到现在为止，<code>Maybe</code>是最常见的表示可能失败的计算的型别了。但有时<code>Maybe</code>也并不是十分的好用，因为<code>Nothing</code>中包含的信息还是太少。要是我们不关心函数失败的原因，它还是不错的。就像<code>Data.Map</code>的<code>lookup</code>只有在搜寻的项不在 Map 时才会失败，对此我们一清二楚。但我们若想知道函数失败的原因，那还得使用<code>Either a b</code>，用<code>a</code>来表示可能的错误的型别，用<code>b</code>来表示一个成功运算的型别。从现在开始，错误一律用<code>Left</code>值构造子，而结果一律用<code>Right</code>。</p><p>一个例子：有个学校提供了不少壁橱，好给学生们地方放他们的 Gun'N'Rose 海报。每个壁橱都有个密码，哪个学生想用个壁橱，就告诉管理员壁橱的号码，管理员就会告诉他壁橱的密码。但如果这个壁橱已经让别人用了，管理员就不能告诉他密码了，得换一个壁橱。我们就用<code>Data.Map</code>的一个 Map 来表示这些壁橱，把一个号码映射到一个表示壁橱占用情况及密码的 Tuple 里。</p><pre class="code">import qualified Data.Map as Map  

data LockerState = Taken | Free deriving (Show, Eq)  

type Code = String  

type LockerMap = Map.Map Int (LockerState, Code)</pre><p>很简单，我们引入了一个新的型别来表示壁橱的占用情况。并为壁橱密码及按号码找壁橱的 Map 分别设置了一个别名。好，现在我们实现这个按号码找壁橱的函数，就用<code>Either String Code</code>型别表示我们的结果，因为<code>lookup</code>可能会以两种原因失败。厨子已经让别人用了或者压根就没有这个橱子。如果<code>lookup</code>失败，就用字元串表明失败的原因。</p><pre class="code">lockerLookup :: Int -> LockerMap -> Either String Code  
lockerLookup lockerNumber map =   
    case Map.lookup lockerNumber map of   
        Nothing -> Left $ "Locker number " ++ show lockerNumber ++ " doesn't exist!"  
        Just (state, code) -> if state /= Taken   
                                then Right code  
                                else Left $ "Locker " ++ show lockerNumber ++ " is already taken!"</pre><p>我们在这里个 Map 中执行一次普通的<code>lookup</code>，如果得到一个<code>Nothing</code>，就返回一个<code>Left String</code>的值，告诉他压根就没这个号码的橱子。如果找到了，就再检查下，看这橱子是不是已经让别人用了，如果是，就返回个<code>Left String</code>说它已经让别人用了。否则就返回个<code>Right Code</code>的值，通过它来告诉学生壁橱的密码。它实际上就是个<code>Right String</code>，我们引入了个型别别名让它这型别声明更好看。</p><p>如下是个 Map 的例子：</p><pre class="code">lockers :: LockerMap  
lockers = Map.fromList   
    [(100,(Taken,"ZD39I"))  
    ,(101,(Free,"JAH3I"))  
    ,(103,(Free,"IQSA9"))  
    ,(105,(Free,"QOTSA"))  
    ,(109,(Taken,"893JJ"))  
    ,(110,(Taken,"99292"))  
    ]</pre><p>现在从里面<code>lookup</code>某个橱子号..</p><pre class="code">ghci> lockerLookup 101 lockers  
Right "JAH3I"  
ghci> lockerLookup 100 lockers  
Left "Locker 100 is already taken!"  
ghci> lockerLookup 102 lockers  
Left "Locker number 102 doesn't exist!"  
ghci> lockerLookup 110 lockers  
Left "Locker 110 is already taken!"  
ghci> lockerLookup 105 lockers  
Right "QOTSA"</pre><p>我们完全可以用<code>Maybe a</code>来表示它的结果，但这样一来我们就对得不到密码的原因不得而知了。而在这里，我们的新型别可以告诉我们失败的原因。</p><a name="Recursive data structures (递回地定义资料结构)"></a><h2>Recursive data structures (递回地定义资料结构)</h2><p>如我们先前看到的，一个 algebraic data type 的构造子可以有好几个 field，其中每个 field 都必须有具体的型态。有了那个概念，我们能定义一个型态，其中他的构造子的 field 的型态是他自己。这样我们可以递回地定义下去，某个型态的值便可能包含同样型态的值，进一步下去他还可以再包含同样型态的值。</p><p>考虑一下 List:<code>[5]</code>。他其实是<code>5:[]</code>的语法糖。在<code>:</code>的左边是一个普通值，而在右边是一串 List。只是在这个案例中是空的 List。再考虑<code>[4,5]</code>。他可以看作<code>4:(5:[])</code>。看看第一个<code>:</code>，我们看到他也有一个元素在左边，一串 List<code>5:[]</code>在右边。同样的道理<code>3:(4:(5:6:[]))</code>也是这样。</p><p>我们可以说一个 List 的定义是要码是空的 List 或是一个元素，后面用<code>:</code>接了另一串 List。</p><p>我们用 algebraic data type 来实作我们自己的 List！</p><pre class="code">data List a = Empty | Cons a (List a) deriving (Show, Read, Eq, Ord)</pre><p>这读起来好像我们前一段提及的定义。他要码是空的 List，或是一个元素跟一串 List 的结合。如果你被搞混了，看看用 record syntax 定义的可能比较清楚。</p><pre class="code">data List a = Empty | Cons { listHead :: a, listTail :: List a} deriving (Show, Read, Eq, Ord)</pre><p>你可能也对这边的<code>Cons</code>构造子不太清楚。<code>cons</code>其实就是指<code>:</code>。对 List 而言，<code>:</code>其实是一个构造子，他接受一个值跟另一串 List 来构造一个 List。现在我们可以使用我们新定义的 List 型态。换句话说，他有两个 field，其中一个 field 具有型态<code>a</code>，另一个有型态<code>[a]</code>。</p><pre class="code">ghci> Empty  
Empty  
ghci> 5 `Cons` Empty  
Cons 5 Empty  
ghci> 4 `Cons` (5 `Cons` Empty)  
Cons 4 (Cons 5 Empty)  
ghci> 3 `Cons` (4 `Cons` (5 `Cons` Empty))  
Cons 3 (Cons 4 (Cons 5 Empty))</pre><p>我们用中缀的方式呼叫<code>Cons</code>构造子，这样你可以很清楚地看到他就是<code>:</code>。<code>Empty</code>代表<code>[]</code>，而<code>4 `Cons` (5 `Cons` Empty)</code>就是<code>4:(5:[])</code>。</p><p>我们可以只用特殊字元来定义函数，这样他们就会自动具有中缀的性质。我们也能同样的手法套用在构造子上，毕竟他们不过是回传型态的函数而已。</p><pre class="code">infixr 5 :-:  
data List a = Empty | a :-: (List a) deriving (Show, Read, Eq, Ord)</pre><p>首先我们留意新的语法结构：fixity 宣告。当我们定义函数成 operator，我们能同时指定 fixity (但并不是必须的)。fixity 指定了他应该是 left-associative 或是 right-associative，还有他的优先顺序。例如说，<code>*</code>的 fixity 是<code>infixl 7 *</code>，而<code>+</code>的 fixity 是<code>infixl 6</code>。代表他们都是 left-associative。<code>(4 * 3 * 2)</code>等于<code>((4 * 3) * 2)</code>。但<code>*</code>拥有比<code>+</code>更高的优先顺序。所以<code>5 * 4 + 3</code>会是<code>(5 * 4) + 3</code>。</p><p>这样我们就可以写成<code>a :-: (List a)</code>而不是<code>Cons a (List a)</code>：</p><pre class="code">ghci> 3 :-: 4 :-: 5 :-: Empty  
(:-:) 3 ((:-:) 4 ((:-:) 5 Empty))  
ghci> let a = 3 :-: 4 :-: 5 :-: Empty  
ghci> 100 :-: a  
(:-:) 100 ((:-:) 3 ((:-:) 4 ((:-:) 5 Empty)))</pre><p>Haskell 在宣告<code>deriving Show</code>的时候，他会仍视构造子为前缀函数，因此必须要用括号括起来。</p><p>我们在来写个函数来把两个 List 连起来。一般<code>++</code>在操作普通 List 的时候是这样的：</p><pre class="code">infixr 5  ++ 
(++) :: [a] -> [a] -> [a]  
[]     ++ ys = ys  
(x:xs) ++ ys = x : (xs ++ ys)</pre><p>我们把他偷过来用在我们的 List 上，把函数命名成<code>.++</code>：</p><pre class="code">infixr 5  .++  
(.++) :: List a -> List a -> List a   
Empty .++ ys = ys  
(x :-: xs) .++ ys = x :-: (xs .++ ys)</pre><p>来看看他如何运作：</p><pre class="code">ghci> let a = 3 :-: 4 :-: 5 :-: Empty  
ghci> let b = 6 :-: 7 :-: Empty  
ghci> a .++ b  
(:-:) 3 ((:-:) 4 ((:-:) 5 ((:-:) 6 ((:-:) 7 Empty))))</pre><p>如果我们想要的话，我们可以定义其他操作我们list的函数。</p><p>注意到我们是如何利用<code>(x :-: xs)</code>做模式匹配的。他运作的原理实际上就是利用到构造子。我们可以利用<code>:-:</code>做模式匹配原因就是他是构造子，同样的<code>:</code>也是构造子所以可以用他做匹配。<code>[]</code>也是同样道理。由于模式匹配是用构造子来作的，所以我们才能对像<code>8</code>,<code>'a'</code>之类的做模式匹配。他们是数值与字元的构造子。</p><p>接下来我们要实作二元搜寻树 (binary search tree)。如果你对二元搜寻树不太清楚，我们来快速地解释一遍。他的结构是每个节点指向两个其他节点，一个在左边一个在右边。在左边节点的元素会比这个节点的元素要小。在右边的话则比较大。每个节点最多可以有两棵子树。而我们知道譬如说一棵包含 5 的节点的左子树，里面所有的元素都会小于 5。而节点的右子树里面的元素都会大于 5。如果我们想找找看 8是不是在我们的树里面，我们就从 5 那个节点找起，由于 8 比 5 要大，很自然地就会往右搜寻。接著我们走到 7，又由于 8 比 7 要大，所以我们再往右走。我们在三步就找到了我们要的元素。如果这不是棵树而是 List 的话，那就会需要花到七步才能找到 8。</p><p><code>Data.Set</code>跟<code>Data.Map</code>中的<code>set</code>和 Map 都是用树来实现的，只是他们是用平衡二元搜寻树而不是随意的二元搜寻树。不过这边我们就只先写一棵普通的二元搜寻树就好了。</p><p>这边我们来定义一棵树的结构：他不是一棵空的树就是带有值并含有两棵子树。听起来非常符合 algebraic data type 的结构！</p><pre class="code">data Tree a = EmptyTree | Node a (Tree a) (Tree a) deriving (Show, Read, Eq)</pre><p>我们不太想手动来建颗二元搜寻树，所以我们要来写一个函数，他接受一棵树还有一个元素，把这个元素安插到这棵二元搜寻树中。当拿这个元素跟树的节点比较结果比较小的话，我们就往左走，如果比较大，就往右走。重复这个动作直到我们走到一棵空的树。一旦碰到空的树的话，我们就把元素插入节点。</p><p>在 C 语言中，我们是用修改指标的方式来达成这件事。但在 Haskell 中，我们没办法修改我们的树。所以我们在决定要往左或往右走的时候就做一棵新的子树，走到最后要安插节点的时候也是做一棵新的树。因此我们插入函数的型态会是<code>a -> Tree a -> Tree a</code>。他接受一个元素跟一棵树，并回传一棵包含了新元素的新的树。这看起来很没效率的样子，但别担心，惰性的特性可以让我们不用担心这个。</p><p>来看下列两个函数。第一个做了一个单节点的树，而第二个插入一个元素到一棵树中。</p><pre class="code">singleton :: a -> Tree a  
singleton x = Node x EmptyTree EmptyTree  
  
treeInsert :: (Ord a) => a -> Tree a -> Tree a  
treeInsert x EmptyTree = singleton x  
treeInsert x (Node a left right)   
      | x == a = Node x left right  
      | x < a  = Node a (treeInsert x left) right  
      | x > a  = Node a left (treeInsert x right)</pre><p><code>singleton</code>函数只是一个做一个含有两棵空子树的节点的函数的别名。在插入的操作中，我们先为终端条件定义了一个模式匹配。如果我们走到了一棵空的子树，这表示我们到达了我们想要的地方，我们便建造一棵空的单元素的树来放在那个位置。如果我们还没走到一棵空的树来插入我们的元素。那就必须要做一些检查来往下走。如果我们要安插的元素跟 root 所含有的元素相等，那就直接回传这棵树。如果安插的元素比较小，就回传一棵新的树。这棵树的 root 跟原来的相同，右子树也相同，只差在我们要安插新的元素到左子树中。如果安插的元素反而比较大，那整个过程就相反。</p><p>接下来，我们要写一个函数来检查某个元素是否已经在这棵树中。首先我们定义终端条件。如果我们已经走到一棵空的树，那这个元素一定不在这棵树中。这跟我们搜寻 List 的情形是一致的。如果我们要在空的 List 中搜寻某一元素，那就代表他不在这个 List 里面。假设我们现在搜寻一棵非空的树，而且 root 中的元素刚好就是我们要的，那就找到了。那如果不是呢？我们就要利用在 root 节点左边的元素都比 root 小的这个性质。如果我们的元素比 root 小，那就往左子树中找。如果比较大，那就往右子树中找。</p><pre class="code">treeElem :: (Ord a) => a -> Tree a -> Bool  
treeElem x EmptyTree = False  
treeElem x (Node a left right)  
    | x == a = True  
    | x < a  = treeElem x left  
    | x > a  = treeElem x right</pre><p>我们要作的就是把之前段落所描述的事转换成程式码。首先我们不想手动一个个来创造一棵树。我们想用一个<code>fold</code>来从一个 List 创造一棵树。要知道走遍一个 List 并回传某种值的操作都可以用<code>fold</code>来实现。我们先从一棵空的树开始，然后从右边走过 List的 每一个元素，一个一个丢到树里面。</p><pre class="code">ghci> let nums = [8,6,4,1,7,3,5]  
ghci> let numsTree = foldr treeInsert EmptyTree nums  
ghci> numsTree  
Node 5 (Node 3 (Node 1 EmptyTree EmptyTree) (Node 4 EmptyTree EmptyTree)) (Node 7 (Node 6 EmptyTree EmptyTree) (Node 8 EmptyTree EmptyTree))</pre><p>在<code>foldr</code>中，<code>treeInsert</code>是做 folding 操作的函数，而<code>EmptyTree</code>是起始的 accumulator，<code>nums</code>则是要被走遍的 List。</p><p>当我们想把我们的树印出来的时候，印出来的形式会不太容易读。但如果我们能有结构地印出来呢？我们知道 root 是 5，他有两棵子树，其中一个的 root 是 3 另一个则是 7。</p><pre class="code">ghci> 8 `treeElem` numsTree  
True  
ghci> 100 `treeElem` numsTree  
False  
ghci> 1 `treeElem` numsTree  
True  
ghci> 10 `treeElem` numsTree  
False</pre><p>检查元素是否属于某棵树的函数现在能正常运作了！</p><p>你可以看到 algebraic data structures 是非常有力的概念。我们可以使用这个结构来构造出布林值，周一到周五的概念，甚至还有二元树。</p><a name="Typeclasses 的第二堂课"></a><h2>Typeclasses 的第二堂课</h2><p>到目前为止我们学到了一些 Haskell 中的标准 typeclass，也学到了某些已经定义为他们 instance 的型别。我们知道如何让我们自己定义的型别自动被 Haskell 所推导成标准 typeclass 的 instance。在这个章节中，我们会学到如何构造我们自己的 typeclass，并且如何构造这些 typeclass 的 type instance。</p><p>来快速复习一下什么是 typeclass: typeclass 就像是 interface。一个 typeclass 定义了一些行为(像是比较相不相等，比较大小顺序，能否穷举)而我们会把希望满足这些性质的型别定义成这些 typeclass 的 instance。typeclass 的行为是由定义的函数来描述。并写出对应的实做。当我们把一个型别定义成某个 typeclass 的 instance，就表示我们可以对那个型别使用 typeclass 中定义的函数。</p><p>Typeclass 跟 Java 或 Python 中的 class 一点关系也没有。这个概念让很多人混淆，所以我希望你先忘掉所有在命令式语言中学到有关 class 的所有东西。</p><p>例如说，<code>Eq</code>这个 typeclass 是描述可以比较相等的事物。他定义了<code>==</code>跟<code>/=</code>两个函数。如果我们有一个型别<code>Car</code>，而且对他们做相等比较是有意义的，那把<code>Car</code>作成是<code>Eq</code>的一个 instance 是非常合理的。</p><p>这边来看看在<code>Prelude</code>之中<code>Eq</code>是怎么被定义的。</p><pre class="code">class Eq a where  
    (==) :: a -> a -> Bool  
    (/=) :: a -> a -> Bool  
    x == y = not (x /= y)  
    x /= y = not (x == y)</pre><p>我们在这边看到了一些奇怪的语法跟关键字。别担心，你一下子就会了解他们的。首先，我们看到<code>class Eq a where</code>，那代表我们定义了一个新的 typeclass 叫做<code>Eq</code>。<code>a</code>是一个型别变数，他代表<code>a</code>是任何我们在定义 instance 时的型别。他不一定要叫做<code>a</code>。他也不一定非要一个字母不可，只要他是小写就好。然后我们又定义了几个函数。我们并不一定要实做函数的本体，不过必须要写出函数的型别宣告。</p><p>如果我们写成<code>class Eq equatable where</code>还有<code>(==) :: equatable -> equatable -> Bool</code>这样的形式，对一些人可能比较容易理解。</p><p>总之我们实做了<code>Eq</code>中需要定义的函数本体，只是我们定义他的方式是用交互递回的形式。我们描述两个<code>Eq</code>的 instance 药箱等，那他们就不能不一样，而他们如果不一样，那他们就是不相等。我们其实不必这样写，但很快你会看到这其实是有用的。</p><p>如果我们说<code>class Eq a where</code>然后定义<code>(==) :: a -> a -> Bool</code>，那我们之后检查函数的型别时会发现他的型别是<code>(Eq a) => a -> a -> Bool</code>。</p><p>所以一旦我们有了 class，那我们能拿他来做些什? 我们可以定义些 type instance，并可以得到些好的性质。来看看下面这个型别：</p><pre class="code">data TrafficLight = Red | Yellow | Green</pre><p>他定义了红绿灯的状态。注意到这边，尽管我们可以用<code>derive` 来自动生成</code>Eq<code>跟</code>Show<code>的 instance，我们并使用他。那是因为我们想要用手写来演练。下面是我们如何定义</code>Eq的 instance 的：</p><pre class="code">instance Eq TrafficLight where  
    Red == Red = True  
    Green == Green = True  
    Yellow == Yellow = True  
    _ == _ = False</pre><p>首先我们使用了<code>instance</code>这个关键字。class 是用来定义新的 typeclass，而 instance 是用来说明我们要定义某个 typeclass 的 instance。当我们要定义<code>Eq</code>，我们会写<code>class Eq a where</code>，其中<code>a</code>代表任何型态。我们可以从 instance 的写法：<code>instance Eq TrafficLight where</code>看出来。我们会把<code>a</code>换成实际的型别。</p><p>由于<code>==</code>是用<code>/=</code>来定义的，同样的<code>/=</code>也是用<code>==</code>来定义。所以我们只需要在 instance 定义中复写其中一个就好了。我们这样叫做定义了一个 minimal complete definition。这是说能让型别符合 class 行为所最小需要实做的函数数量。而<code>Eq</code>的 minimal complete definition 需要<code>==</code>或<code>/=</code>其中一个。而如果<code>Eq</code>是这样定义的：</p><pre class="code">class Eq a where  
    (==) :: a -> a -> Bool  
    (/=) :: a -> a -> Bool</pre><p>当我们定义 instance 的时候必须要两个函数都实做，因为 Haskell 并不知道这两个函数是怎么关联在一起的。所以 minimal complete definition 在这边是<code>==</code>跟<code>/=</code>。</p><p>你可以看到我们是用模式匹配来实做<code>==</code>。由于不相等的情况比较多，所以我们只写出相等的，最后再用一个<code>case</code>接住说你不在前面相等的<code>case</code>的话，那就是不相等。</p><p>我们再来写<code>Show</code>的 instance。要满足<code>Show</code>的 minimal complete definition，我们必须实做<code>show</code>函数，他接受一个值并把他转成字串。</p><pre class="code">instance Show TrafficLight where  
    show Red = "Red light"  
    show Yellow = "Yellow light"  
    show Green = "Green light"</pre><p>再一次地，我们用模式匹配来完成我们的任务。我们来看看他是如何运作的。</p><pre class="code">ghci> Red == Red  
True  
ghci> Red == Yellow  
False  
ghci> Red `elem` [Red, Yellow, Green]  
True  
ghci> [Red, Yellow, Green]  
[Red light,Yellow light,Green light]</pre><p>如果我们用<code>derive</code>来自动产生<code>Eq</code>的话，效果是一样的。不过用<code>derive</code>来产生<code>show</code>的话，他会把值构造子转换成字串。但我们这边要的不太一样，我们希望印出像<code>"Red light"</code>这样的字串，所以我们就必须手动来写出 instance。</p><p>你也可以把 typeclass 定义成其他 typeclass 的 subclass。像是<code>Num</code>的 class 宣告就有点冗长，但我们先看个雏型。</p><pre class="code">class (Eq a) => Num a where  
   ...</pre><p>正如我们先前提到的，我们可以在很多地方加 上class constraints。这不过就是在<code>class Num a where</code>中的<code>a</code>上，加上他必需要是<code>Eq</code>的 instance 的限制。这基本上就是在说我们在定义一个型别为<code>Num</code>之前，必须先为他定义<code>Eq</code>的 instance。在某个型别可以被视作<code>Number</code>之前，必须先能被比较相不相等其实是蛮合理的。这就是 subclass 在做的事：帮 class declaration 加上限制。也就是说当我们定义 typeclass 中的函数本体时，我们可以预设<code>a</code>是属于<code>Eq</code>，因此能使用<code>==</code>。</p><p>但像是<code>Maybe</code>或是 List 是如何被定义成 typeclass 的 instance 呢？<code>Maybe</code>的特别之处在于他跟<code>TrafficLight</code>不一样，他不是一个具体的型别。他是一个型别构造子，接受一个型别参数(像是<code>Char</code>)而构造出一个具体的型别(像是<code>Maybe Char</code>)我们来看一下<code>Eq</code>这个 typeclass：</p><pre class="code">class Eq a where  
    (==) :: a -> a -> Bool  
    (/=) :: a -> a -> Bool  
    x == y = not (x /= y)  
    x /= y = not (x == y)</pre><p>从型别宣告来看，可以看到<code>a</code>必须是一个具体型别，因为所有在函数中的型别都必须是具体型别。(你没办法写一个函数，他的型别是<code>a -> Maybe</code>，但你可以写一个函数，他的型别是<code>a -> Maybe a</code>，或是<code>Maybe Int -> Maybe String</code>) 这就是为什? 我们不能写成像这样：</p><pre class="code">instance Eq Maybe where  
    ...</pre><pre class="code">instance Eq (Maybe m) where  
    Just x == Just y = x == y  
    Nothing == Nothing = True  
    _ == _ = False</pre><p>这就好像在说我们要把<code>Maybe something</code>这种东西全部都做成<code>Eq</code>的 instance。我们的确可以写成<code>(Maybe something)</code>，但我们通常是只用一个字母，这样比较像是 Haskell 的风格。<code>(Maybe m)</code>这边则取代了<code>a</code>在<code>class Eq a where</code>的位置。尽管<code>Maybe</code>不是一个具体的型别。<code>Maybe m</code>却是。指定一个型别参数(在这边是小写的<code>m</code>)，我们说我们想要所有像是<code>Maybe m</code>的都成为<code>Eq</code>的 instance。</p><p>不过这仍然有一个问题。你能看出来吗？ 我们用<code>==</code>来比较<code>Maybe</code>包含的东西，但我们并没有任何保证说<code>Maybe</code>装的东西可以是<code>Eq</code>。这就是为什? 我们需要修改我们的 instance 定义：</p><pre class="code">instance (Eq m) => Eq (Maybe m) where  
    Just x == Just y = x == y  
    Nothing == Nothing = True  
    _ == _ = False</pre><p>这边我们必须要加上限制。在这个 instance 的宣告中，我们希望所有<code>Maybe m</code>形式的型别都属于<code>Eq</code>，但只有当<code>m</code>也属于<code>Eq</code>的时候。这也是 Haskell 在<code>derive</code>的时候做的事。</p><p>在大部份情形下，在 typeclass 宣告中的 class constraints 都是要让一个 typeclass 成为另一个 typeclass 的 subclass。而在 instance 宣告中的 class constraint 则是要表达型别的要求限制。举里来说，我们要求<code>Maybe</code>的内容物也要属于<code>Eq</code>。</p><p>当定义 instance 的时候，如果你需要提供具体型别(像是在<code>a -> a -> Bool</code>中的<code>a</code>)，那你必须要加上括号跟型别参数来构造一个具体型别。</p><p>要知道你在定义 instnace 的时候，型别参数会被取代。<code>class Eq a where</code>中的<code>a</code>会被取代成真实的型别。所以试著想像把型别放进型别宣告中。<code>(==) :: Maybe -> Maybe -> Bool</code>并非合法。但<code>(==) :: (Eq m) => Maybe m -> Maybe m -> Bool</code>则是。这是不论我们要定义什么，通用的型别宣告都是<code>(==) :: (Eq a) => a -> a -> Bool</code></p><p>还有一件事要确认。如果你想看看一个 typeclass 有定义哪些 instance。可以在 ghci 中输入<code>:info YourTypeClass</code>。所以输入<code>info Num</code>会告诉你这个 typeclass 定义了哪些函数，还有哪些型别属于这个 typeclass。<code>:info</code>也可以查询型别跟型别构造子的资讯。如果你输入<code>:info Maybe</code>。他会显示<code>Maybe</code>所属的所有 typeclass。<code>:info</code>也能告诉你函数的型别宣告。</p><a name="yes-no typeclass"></a><h2>yes-no typeclass</h2><p>在 Javascript 或是其他弱型别的程式语言，你能在 if expression 钟摆上任何东西。举例来说，你可以做像下列的事：</p><p><code>if (0) alert("YEAH!") else alert("NO!")</code>,</p><p><code>if ("") alert ("YEAH!") else alert("NO!")</code>,</p><p><code>if (false) alert("YEAH") else alert("NO!)</code>等等，</p><p>而上述所有的片段执行后都会跳出<code>NO!</code>。如果你写<code>if ("WHAT") alert ("YEAH") else alert("NO!")</code>，他会跳出<code>YEAH!</code>。因为 Javascript 认为非空字串会是<code>true</code>。</p><p>尽管使用<code>Bool</code>来表达布林的语意是比较好的作法。为了有趣起见，我们来试试看模仿 Javascript 的行为。我们先从 typeclass 宣告开始看：</p><pre class="code">class YesNo a where  
    yesno :: a -> Bool</pre><p><code>YesNo</code>typeclass 定义了一个函数。这个函数接受一个可以判断为真否的型别的值。而从我们写<code>a</code>的位置，可以看出来<code>a</code>必须是一个具体型别。</p><p>接下来我们来定义一些 instance。对于数字，我们会假设任何非零的数字都会被当作<code>true</code>，而 0 则当作<code>false</code>。</p><pre class="code">instance YesNo Int where  
    yesno 0 = False  
    yesno _ = True</pre><p>空的 List (包含字串)代表<code>false</code>，而非空的 List 则代表<code>true</code>。</p><pre class="code">instance YesNo [a] where  
    yesno [] = False  
    yesno _ = True</pre><p>留意到我们加了一个型别参数<code>a</code>来让整个 List 是一个具体型别，不过我们并没有对包涵在 List 中的元素的型别做任何额外假设。我们还剩下<code>Bool</code>可以被作为真假值，要定义他们也很容易：</p><pre class="code">instance YesNo Bool where  
    yesno = id</pre><p>你说<code>id</code>是什么？他不过是标准函式库中的一个函数，他接受一个参数并回传相同的东西。</p><p>我们也让<code>Maybe a</code>成为<code>YesNo</code>的 instance。</p><pre class="code">instance YesNo (Maybe a) where  
    yesno (Just _) = True  
    yesno Nothing = False</pre><p>由于我们不需要对于<code>Maybe</code>的内容做任何假设，因此不需要 class constraints。我们只说他如果是<code>Just</code>就是<code>true</code>，如果是<code>Nothing</code>就是<code>false</code>。我们仍和必须要写成<code>(Maybe a)</code>而不是<code>Maybe</code>。毕竟你想想看，如果<code>Maybe -> Bool</code>是不合理的，而<code>Maybe a -> Bool</code>却是合理的。有了这个定义，现在<code>Maybe something</code>都属于<code>YesNo</code>，不论<code>somehing</code>是什?。</p><p>之前我们定义了<code>Tree a</code>，那代表一个二元搜寻树。我们可以说一棵空的树是<code>false</code>，而非空的树则是<code>true</code>。</p><pre class="code">instance YesNo (Tree a) where  
    yesno EmptyTree = False  
    yesno _ = True</pre><p>而一个红绿灯可以代表 yes or no 吗？当然可以。如果他是红灯，那你就会停下来，如果他是绿灯，那你就能走。但如果是黄灯呢？只能说我通常会闯黄灯。</p><pre class="code">instance YesNo TrafficLight where  
    yesno Red = False  
    yesno _ = True</pre><p>现在我们定义了许多 instance，来试著跑跑看！</p><pre class="code">ghci> yesno $ length []  
False  
ghci> yesno "haha"  
True  
ghci> yesno ""  
False  
ghci> yesno $ Just 0  
True  
ghci> yesno True  
True  
ghci> yesno EmptyTree  
False  
ghci> yesno []  
False  
ghci> yesno [0,0,0]  
True  
ghci> :t yesno  
yesno :: (YesNo a) => a -> Bool</pre><p>很好，统统是我们预期的结果。我们来写一个函数来模仿 if statement 的行为，但他是运作在<code>YesNo</code>的型别上。</p><pre class="code">yesnoIf :: (YesNo y) => y -> a -> a -> a  
yesnoIf yesnoVal yesResult noResult = if yesno yesnoVal then yesResult else noResult</pre><p>很直觉吧！他接受一个 yes or no 的值还有两个部份，如果值是代表 "yes"，那第一个部份就会被执行，而如果值是 "no"，那第二个部份就会执行。</p><pre class="code">ghci> yesnoIf [] "YEAH!" "NO!"  
"NO!"  
ghci> yesnoIf [2,3,4] "YEAH!" "NO!"  
"YEAH!"  
ghci> yesnoIf True "YEAH!" "NO!"  
"YEAH!"  
ghci> yesnoIf (Just 500) "YEAH!" "NO!"  
"YEAH!"  
ghci> yesnoIf Nothing "YEAH!" "NO!"  
"NO!"</pre><a name="Functor typeclass"></a><h2>Functor typeclass</h2><p>到目前为止我们看过了许多在标准函式库中的 typeclass。我们操作过<code>Ord</code>，代表可以被排序的东西。我们也操作过<code>Eq</code>，代表可以被比较相等性的事物。也看过<code>Show</code>，代表可以被印成字串来表示的东西。至于<code>Read</code>则是我们可以把字串转换成型别的动作。不过现在我们要来看一下<code>Functor</code>这个 typeclass，基本上就代表可以被 map over 的事物。听到这个词你可能会联想到 List，因为 map over list 在 Haskell 中是很常见的操作。你没想错，List 的确是属于<code>Functor</code>这个 typeclass。</p><p>来看看他的实做会是了解<code>Functor</code>的最佳方式：</p><pre class="code">class Functor f where  
    fmap :: (a -> b) -> f a -> f b</pre><p>我们看到他定义了一个函数<code>fmap</code>，而且并没有提供一个预设的实作。<code>fmap</code>的型别蛮有趣的。到目前为止的我们看过的 typeclass 中的型别变数都是具体型别。就像是<code>(==) :: (Eq a) => a -> a -> Bool</code>中的<code>a</code>一样。但现在碰到的<code>f</code>并不是一个具体型别(一个型别像是<code>Int</code>,<code>Bool</code>或<code>Maybe String</code>) 但一个型别构造子接受一个型别参数。如果要快速回顾的话可以看一下<code>Maybe Int</code>是一个具体型别，但<code>Maybe</code>是一个型别构造子，他接受一个型别来作为参数。总之，我们知道<code>fmap</code>接受一个函数，这个函数从一个型别映射到令一个型别，还接受一个 functor 装有原始的型别，然后会回传一个 functor 装有映射后的型别。</p><p>如果听不太懂也没关系。当我们看几个范例之后会比较好懂。不过这边<code>fmap</code>的型别宣告让我们想起类似的东西。就是<code>map :: (a -> b) -> [a] -> [b]</code>。</p><p>他接受一个函数，这函数把一个型别的东西映射成另一个。还有一串装有某个型别的 List 变成装有另一个型别的 List。到这边听起来实在太像 functor 了。实际上，<code>map</code>就是针对 List 的<code>fmap</code>。来看看 List 是如何被定义成<code>Functor</code>的 instance 的。</p><pre class="code">instance Functor [] where  
    fmap = map</pre><p>注意到我们不是写成<code>instance Functor [a] where</code>，因为从<code>fmap :: (a -> b) -> f a -> f b</code>我们知道<code>f</code>是一个型别构造子，他接受一个型别。而<code>[a]</code>则已经是一个具体型别(一个用有某个型别的 List)，尽管<code>[]</code>是一个型别构造子，他接受某个型别而构造出像<code>[Int]</code>,<code>[String]</code>甚至是<code>[[String]]</code>。</p><p>对于 List，<code>fmap</code>只不过是<code>map</code>，对 List 操作的时候他们都是一样的。</p><pre class="code">map :: (a -> b) -> [a] -> [b]  
ghci> fmap (*2) [1..3]  
[2,4,6]  
ghci> map (*2) [1..3]  
[2,4,6]</pre><p>至于当我们对空的 List 操作<code>map</code>或<code>fmap</code>呢？我们会得到一个空的 List。他把一个型别为<code>[a]</code>的空的 List 转成型别为<code>[b]</code>的空的 List。</p><p>可以当作盒子的型别可能就是一个 functor。你可以把 List 想做是一个拥有无限小隔间的盒子。他们可能全部都是空的，已也可能有一部份是满的其他是空的。所以作为一个盒子会具有什么性质呢？例如说<code>Maybe a</code>。他表现得像盒子在于他可能什么东西都没有，就是<code>Nothing</code>，或是可以装有一个东西，像是<code>"HAHA"</code>，在这边就是<code>Just "HAHA"</code>。可以看到<code>Maybe</code>作为一个 functor 的定义：</p><pre class="code">instance Functor Maybe where  
    fmap f (Just x) = Just (f x)  
    fmap f Nothing = Nothing</pre><p>注意到我们是写<code>instance Functor Maybe where</code>而不是<code>instance Functor (Maybe m) where</code>，就像我们在写<code>YesNo</code>时的<code>Maybe</code>一样。<code>Functor</code>要的是一个接受一个型别参数的型别构造子而不是一个具体型别。如果你把<code>f</code>代换成<code>Maybe</code>。<code>fmap</code>就会像<code>(a -> b) -> Maybe a -> Maybe b</code>。但如果你把<code>f</code>代换成<code>(Maybe m)</code>，那他就会像<code>(a -> b) -> Maybe m a -> Maybe m b</code>，这看起来并不合理，因为<code>Maybe</code>只接受一个型别参数。</p><p>总之，<code>fmap</code>的实作是很简单的。如果一个空值是<code>Nothing</code>，那他就会回传<code>Nothing</code>。如果我们 map over 一个空的盒子，我们就会得到一个空的盒子。就像我们 map over 一个空的 List，那我们就会得到一个空的 List。如果他不是一个空值，而是包在<code>Just</code>中的某个值，那我们就会套用在包在<code>Just</code>中的值。</p><pre class="code">ghci> fmap (++ " HEY GUYS IM INSIDE THE JUST") (Just "Something serious.")  
Just "Something serious. HEY GUYS IM INSIDE THE JUST"  
ghci> fmap (++ " HEY GUYS IM INSIDE THE JUST") Nothing  
Nothing  
ghci> fmap (*2) (Just 200)  
Just 400  
ghci> fmap (*2) Nothing  
Nothing</pre><p>另外<code>Tree a</code>的型别也可以被 map over 且被定义成<code>Functor</code>的一个 instnace。他可以被想成是一个盒子，而<code>Tree</code>的型别构造子也刚好接受单一一个型别参数。如果你把<code>fmap</code>看作是一个特别为<code>Tree</code>写的函数，他的型别宣告会长得像这样<code>(a -> b) -> Tree a -> Tree b</code>。不过我们在这边会用到递回。map over 一棵空的树会得到一棵空的树。map over 一棵非空的树会得到一棵被函数映射过的树，他的 root 会先被映射，然后左右子树都分别递回地被函数映射。</p><pre class="code">instance Functor Tree where  
    fmap f EmptyTree = EmptyTree  
    fmap f (Node x leftsub rightsub) = Node (f x) (fmap f leftsub) (fmap f rightsub)</pre><pre class="code">ghci> fmap (*2) EmptyTree  
EmptyTree  
ghci> fmap (*4) (foldr treeInsert EmptyTree [5,7,3,2,1,7])  
Node 28 (Node 4 EmptyTree (Node 8 EmptyTree (Node 12 EmptyTree (Node 20 EmptyTree EmptyTree)))) EmptyTree</pre><p>那<code>Eitehr a b</code>又如何？他可以是一个 functor 吗？<code>Functor</code>限制型别构造子只能接受一个型别参数，但<code>Either</code>却接受两个。幒明得你会想到我可以 partial apply<code>Either</code>，先喂给他一参数，并把另一个参数当作 free parameter。来看看<code>Either a</code>在标准函式库中是如何被定义的：</p><pre class="code">instance Functor (Either a) where  
    fmap f (Right x) = Right (f x)  
    fmap f (Left x) = Left x</pre><p>我们在这边做了些什么？你可以看到我们把<code>Either a</code>定义成一个 instance，而不是<code>Either</code>。那是因为<code>Either a</code>是一个接受单一型别参数的型别构造子，而<code>Either</code>则接受两个。如果<code>fmap</code>是针对<code>Either a</code>，那他的型别宣告就会像是<code>(b -> c) -> Either a b -> Either a c</code>，他又等价于<code>(b -> c) -> (Either a) b -> (Either a) c</code>。在实作中，我们碰到一个<code>Right</code>的时候会做<code>map</code>，但在碰到<code>Left</code>的时候却不这样做，为什么呢？如果我们回头看看<code>Either a b</code>是怎么定义的：</p><pre class="code">data Either a b = Left a | Right b</pre><p>如果我们希望对他们两个都做<code>map</code>的动作，那<code>a</code>跟<code>b</code>必须要是相同的型别。也就是说，如果我们的函数是接受一个字串然后回传另一个字串，而且<code>b</code>是字串，<code>a</code>是数字，这样的情形是不可行的。而且从观察<code>fmap</code>的型别也可以知道，当他运作在<code>Either</code>上的时候，第一个型别参数必须固定，而第二个则可以改变，而其中第一个参数正好就是<code>Left</code>用的。</p><p>我们持续用盒子的比喻也仍然贴切，我们可以把<code>Left</code>想做是空的盒子在他旁边写上错误讯息，说明为什么他是空的。</p><p>在<code>Data.Map</code>中的 Map 也可以被定义成 functor，像是<code>Map k v</code>的情况下，<code>fmap</code>可以用<code>v -> v'</code>这样一个函数来 map over<code>Map k v</code>，并回传<code>Map k v'</code>。</p><p>注意到<code>'</code>在这边并没有特别的意思，他只是用来表示他跟另一个东西有点像，只有一点点差别而已。</p><p>你可以自己试试看把<code>Map k</code>变成<code>Functor</code>的一个 instnace。</p><p>看过了<code>Functor</code>这个 typeclass，我们知道 typeclass 可以拿来代表高阶的概念。我们也练习过不少 partially applying type 跟定义 instance。在下几章中，我们也会看到 functor 必须要遵守的定律。</p><p>还有一件事就是 functor 应该要遵守一些定律，这样他们的一些性质才能被保证。如果我们用<code>fmap (+1)</code>来 map over<code>[1,2,3,4]</code>，我们会期望结果会是<code>[2,3,4,5]</code>而不是反过来变成<code>[5,4,3,2]</code>。如果我们使用<code>fmap (\a -> a)</code>来 map over 一个 list，我们会期待拿回相同的结果。例如说，如果我们给<code>Tree</code>定义了错误的 functor instance，对 tree 使用<code>fmap</code>可能会导致二元搜寻树的性质丧失，也就是在 root 左边的节点不再比 root 小，在 root 右边的节点不再比 root 大。我们在下面几章会多谈 functor laws。</p><a name="Kind"></a><h2>Kind</h2><p>型别构造子接受其他型别作为他的参数，来构造出一个具体型别。这样的行为会让我们想到函数，也是接受一个值当作参数，并回传另一个值。我们也看过型别构造子可以 partially apply (<code>Either String</code>是一个型别构造子，他接受一个型别来构造出一个具体型别，就像<code>Either String Int</code>))。这些都是函数能办到的事。在这个章节中，对于型别如何被套用到型别构造子上，我们会来看一下正式的定义。就像我们之前是用函数的型别来定义出函数是如何套用值得。如果你看不懂的话，你可以跳过这一章，这不会影响你后续的阅读。然而如果你搞懂的话，你会对于型别系统有更进一步的了解。</p><p>像是<code>3</code>,<code>"YEAH"</code>或是<code>takeWhile</code>的值他们都有自己的型别(函数也是值的一种，我们可以把他们传来传去)型别是一个标签，值会把他带著，这样我们就可以推测出他的性质。但型别也有他们自己的标签，叫做 kind。kind 是型别的型别。虽然听起来有点玄妙，不过他的确是个有趣的概念。</p><p>那kind可以拿来做什么呢？我们可以在 ghci 中用<code>:k</code>来得知一个型别的 kind。</p><pre class="code">ghci> :k Int  
Int :: *</pre><p>一个星星代表的是什么意思？一个<code>*</code>代表这个型别是具体型别。一个具体型别是没有任何型别参数，而值只能属于具体型别。而<code>*</code>的读法叫做 star 或是 type。</p><p>我们再看看<code>Maybe</code>的 kind：</p><pre class="code">ghci> :k Maybe  
Maybe :: * -> *</pre><p><code>Maybe</code>的型别构造子接受一个具体型别(像是<code>Int</code>)然后回传一个具体型别，像是<code>Maybe Int</code>。这就是 kind 告诉我们的资讯。就像<code>Int -> Int</code>代表这个函数接受<code>Int</code>并回传一个<code>Int</code>。<code>* -> *</code>代表这个型别构造子接受一个具体型别并回传一个具体型别。我们再来对<code>Maybe</code>套用型别参数后再看看他的 kind 是什么：</p><pre class="code">ghci> :k Maybe Int  
Maybe Int :: *</pre><p>正如我们预期的。我们对<code>Maybe</code>套用了型别参数后会得到一个具体型别(这就是<code>* -> *</code>的意思)这跟<code>:t isUpper</code>还有<code>:t isUpper 'A'</code>的差别有点类似。<code>isUpper</code>的型别是<code>Char -> Bool</code>而<code>isUpper 'A'</code>的型别是<code>Bool</code>。而这两种型别，都是<code>*</code>的 kind。</p><p>我们对一个型别使用<code>:k</code>来得到他的 kind。就像我们对型别使用<code>:t</code>来得到的他的型别一样。就像我们先前说的，型别是值的标签，而 kind 是型别的标签。</p><p>我们再来看看其他的 kind</p><pre class="code">ghci> :k Either  
Either :: * -> * -> *</pre><p>这告诉我们<code>Either</code>接受两个具体型别作为参数，并构造出一个具体型别。他看起来也像是一个接受两个参数并回传值的函数型别。型别构造子是可以做 curry 的，所以我们也能 partially apply。</p><pre class="code">ghci> :k Either String  
Either String :: * -> *  
ghci> :k Either String Int  
Either String Int :: *</pre><p>当我们希望定义<code>Either</code>成为<code>Functor</code>的 instance 的时候，我们必须先 partial apply，因为<code>Functor</code>预期有一个型别参数，但<code>Either</code>却有两个。也就是说，<code>Functor</code>希望型别的 kind 是<code>* -> *</code>，而我们必须先 partial apply<code>Either</code>来得到 kind<code>* -> *</code>，而不是最开始的<code>* -> * -> *</code>。我们再来看看<code>Functor</code>的定义</p><pre class="code">class Functor f where   
    fmap :: (a -> b) -> f a -> f b</pre><p>我们看到<code>f</code>型别变数是接受一个具体型别且构造出一个具体型别的型别。</p><p>知道他构造出具体型别是因为是作为函数参数的型别。</p><p>从那里我们可以推测出一个型别要是属于<code>Functor</code>必须是<code>* -> *</code>kind。</p><p>现在我们来练习一下。来看看下面这个新定义的 typeclass。</p><pre class="code">class Tofu t where  
    tofu :: j a -> t a j</pre><p>这看起来很怪。我们干嘛要为这个奇怪的 typeclass 定义 instance？我们可以来看看他的 kind 是什么？由于<code>j a</code>被当作<code>tofu</code>这个函数的参数的型别，所以<code>j a</code>一定是<code>*</code>kind。我们假设<code>a</code>是<code>*</code>kind，那<code>j</code>就会是<code>* -> *</code>的 kind。我们看到<code>t</code>由于是函数的回传值，一定是接受两个型别参数的型别。而知道<code>a</code>是<code>*</code>，<code>j</code>是<code>* -> *</code>，我们可以推测出<code>t</code>是<code>* -> (* -> *) -> *</code>。也就是说他接受一个具体型别<code>a</code>，一个接受单一参数的型别构造子<code>j</code>，然后产生出一个具体型别。</p><p>我们再来定义出一个型别具有<code>* -> (* -> *) -> *</code>的 kind，下面是一种定义的方法：</p><pre class="code">data Frank a b  = Frank {frankField :: b a} deriving (Show)</pre><p>我们怎么知道这个型别具有<code>* -> (* -> *) -> *</code>的 kind 呢？ADT 中的栏位是要来塞值的，所以他们必须是<code>*</code>kind。我们假设<code>a</code>是<code>*</code>，那<code>b</code>就是接受一个型别参数的kind<code>* -> *</code>。现在我们知道<code>a</code>跟<code>b</code>的 kind 了，而他们又是<code>Frank</code>的型别参数，所以我们知道<code>Frank</code>会有<code>* -> (* -> *) -> *</code>的 kind。第一个<code>*</code>代表<code>a</code>，而<code>(* -> *)</code>代表<code>b</code>。我们构造些<code>Frank</code>的值并检查他们的型别吧：</p><pre class="code">ghci> :t Frank {frankField = Just "HAHA"}  
Frank {frankField = Just "HAHA"} :: Frank [Char] Maybe  
ghci> :t Frank {frankField = Node 'a' EmptyTree EmptyTree}  
Frank {frankField = Node 'a' EmptyTree EmptyTree} :: Frank Char Tree  
ghci> :t Frank {frankField = "YES"}  
Frank {frankField = "YES"} :: Frank Char []</pre><p>由于<code>frankField</code>具有<code>a b</code>的型别。他的值必定有一个类似的型别。他们可能是<code>Just "HAHA"</code>，也就有<code>Maybe [Char]</code>的型别，或是他们可能是<code>['Y','E','S']</code>，他的型别是<code>[Char]</code>。(如果我们是用自己定义的 List 型别的话，那就会是<code>List Char</code>)。我们看到<code>Frank</code>值的型别对应到<code>Frank</code>的 kind。<code>[Char]</code>具有<code>*</code>的 kind，而<code>Maybe</code>则是<code>* -> *</code>。由于结果必须是个值，也就是他必须要是具体型别，因使他必须 fully applied，因此每个<code>Frank blah blaah</code>的值都会是<code>*</code>的 kind。</p><p>要把<code>Frank</code>定义成<code>Tofu</code>的 instance 也是很简单。我们看到<code>tofu</code>接受<code>j a</code>(例如<code>Maybe Int</code>) 并回传<code>t a j</code>。所以我们用<code>j</code>取代<code>Frank</code>，那结果就是<code>Frank Int Maybe</code>。</p><pre class="code">instance Tofu Frank where  
    tofu x = Frank x</pre><pre class="code">ghci> tofu (Just 'a') :: Frank Char Maybe  
Frank {frankField = Just 'a'}  
ghci> tofu ["HELLO"] :: Frank [Char] []  
Frank {frankField = ["HELLO"]}</pre><p>这并不是很有用，但让我们做了不少型别的练习。再来看看下面的型别：</p><pre class="code">data Barry t k p = Barry { yabba :: p, dabba :: t k }</pre><p>我们想要把他定义成<code>Functor</code>的 instance。<code>Functor</code>希望是<code>* -> *</code>的型别，但<code>Barry</code>并不是那种 kind。那<code>Barry</code>的 kind 是什么呢？我们可以看到他接受三个型别参数，所以会是<code>something -> something -> something -> *</code>。<code>p</code>是一个具体型别因此是<code>*</code>。至于<code>k</code>，我们假设他是<code>*</code>，所以<code>t</code>会是<code>* -> *</code>。现在我们把这些代入<code>something</code>，所以 kind 就变成<code>(* -> *) -> * -> * -> *</code>。我们用 ghci 来检查一下。</p><pre class="code">ghci> :k Barry  
Barry :: (* -> *) -> * -> * -> *</pre><p>我们猜对了！现在要把这个型别定义成<code>Functor</code>，我们必须先 partially apply 头两个型别参数，这样我们就会是<code>* -> *</code>的 kind。这代表 instance 定义会是<code>instance Functor (Barry a b) where</code>。如果我们看<code>fmap</code>针对<code>Barry</code>的型别，也就是把<code>f</code>代换成<code>Barry c d</code>，那就会是<code>fmap :: (a -> b) -> Barry c d a -> Barry c d b</code>。第三个<code>Barry</code>的型别参数是对于任何型别，所以我们并不牵扯进他。</p><pre class="code">instance Functor (Barry a b) where  
    fmap f (Barry {yabba = x, dabba = y}) = Barry {yabba = f x, dabba = y}</pre><p>我们把<code>f</code><code>map</code>到第一个栏位。</p><p>在这一个章节中，我们看到型别参数是怎么运作的，以及正如我们用型别来定义出函数的参数，我们也用 kind 是来定义他。</p><p>我们看到函数跟型别构造子有许多彼此相像的地方。然而他们是两个完全不同的东西。当我们在写一般实用的 Haskell 程式时，你几乎不会碰到需要动到 kind 的东西，也不需要动脑去推敲 kind。通常你只需要在定义 instance 时 partially apply 你自己的<code>* -> *</code>或是<code>*</code>型别，但知道背后运作的原理也是很好的。知道型别本身也有自己的型别也是很有趣的。如果你实在不懂这边讲的东西，也可以继续阅读下去。但如果你能理解，那你就会理解 Haskell 型别系统的一大部份。</p>
            <ul class="nav">
                <li class="left">  <img src="../img/prv.png"></img><a href="modules.html">模组 (Modules)</a></li>
                <li class="center"><a href="chapters.html">Index</a></li>
                <li class="right"> <a href="input-and-output.html">输入与输出</a><img src="../img/nxt.png"></img></li>
            </ul>
        </div>
        <div id="footer">
        </div>
        <div id="feedback">Send feedback</div>
    </body>
</html>
